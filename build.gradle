import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
}

application {
    getMainClass().set(System.getProperty("main", "com.cas.main.CAS"))
}

group 'com.math'
version '1.0-SNAPSHOT'

ext {
    lwjglVersion = "3.2.3"
    imguiVersion = '1.83.1'
}

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.imguiNatives = "natives-linux"
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
                ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
                : "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.imguiNatives = "natives-macos"
        project.ext.lwjglNatives = "natives-macos"
        applicationDefaultJvmArgs = ["-XstartOnFirstThread"]
        break
    case OperatingSystem.WINDOWS:
        project.ext.imguiNatives = "natives-windows"
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.contains("64")
                ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                : "natives-windows-x86"
        break
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

    // https://mvnrepository.com/artifact/net.jafama/jafama
    implementation group: 'net.jafama', name: 'jafama', version: '2.1.0'

    // LWJGL
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"

    // jLatexMath
    implementation 'org.scilab.forge:jlatexmath:1.0.7'

    // Dear ImGUI
    implementation "io.github.spair:imgui-java-binding:$imguiVersion"
    implementation "io.github.spair:imgui-java-lwjgl3:$imguiVersion"
    implementation "io.github.spair:imgui-java-$imguiNatives:$imguiVersion"
}

test {
    useJUnitPlatform()
}